// SPDX-License-Identifier: MIT

/**
ccloool:::cc::::cc:::::cccccloooooool;;loddc...............,:'....''...,cc...''...,ll.................';:;,',,'',,;,';:;;:lll::clc;;;;;
ccccllc:::cc:::::::::::c:clccllloolllooooodo,.............,loc'.......:dkkl,;odooodxxl:;;'..............',...........''.';ccc:;:c;....'
::::::::::cc::::::;;:::cclllcccloooollloooddl:,,;:,...';clooddo,.....,lxkkxxxkkkxxxxxddxxdoc,.':cllc;'.....''.''..........,,,'.''......
::::::;;:;::;;;;;;;::::ccclllccllllllloooooooooooool:clddddddddo:..';ldxkkxxxxxxxxxxdxxdddxxdodxxxxxxxo:,'cdddxxdolldl::,.........;clc'
:;;;;;:;:;:;;;;:::;;:::::cclccllccllllddolodoooooddododddodoodxddoodddoxkxxxxxxxdxxddxxdooddoddxkxxkkkkkxdkOxxkOOkkOOkkko:,'';c:';dkOkd
;,,,;;;;::;;,,;;::;,;:;;::cccccccllllllolooooooooooooollooloooodddoooddxkxddxxddddxddxxoodddddddxxxkkkkkkxkkxkkkkkkxkOkxkOOxxkkkxxkkOOO
,,,,;;,;;;,,,,;;;;;,;;;;;::::ccclollccclloollllloolllllcccclooodoodddddddxdodddddddddddddddddooodddxkxkOkxkxxkOkkkxxkkOkkkOOOkkOOkxkkkO
',,,,,',,,,,,,,,,'',,,,',,;::ccccccclcccloollolllolllcclccllccloooddddoodxddxddxdoooododdoloolodxdddxxxkkkxxkkOkkkkkkxxdxkkkOOkkOkxkkxk
'',,,'''''''''''....'''''',;::::::cc:c::clc:clllllllllcccc:ccclloodxdoodooooddddoooooooooollooooodxxxkxkkxkxkkkkkkxxkxxdxkkkkxxkkkxkOkx
'''''..'''...''........''..',,,,;:c:;;;;:c::cccccclllcccc::::cclllodooloolloddooolllloololcclollddddxkxddkkxxkkkxxxxkkkkxxkkxxdkOkkxxkO
.............................''',,,,,,,;::::::cc::::ccc::;;;;;::clccccllollllooollccccllccccclllooooodxddxxxxkxxxxxxxkkkkxxxdxxxkkxxxxx
.......................................',,;:;;;:;;;;;:;;,,,,,,;;:cc:::cllc:cccllcccc:;:c::::ccccllooodddddkkxxxxddxxxxxxxxxddxxxxkkxxxx
..........................................''',,,,;;,,,''......'',;;,,,;:::;:::cc::::;:::;;;;;;;:ccllooolododddxdddxxxddxddxxxdodxxxxxdd
..................'',,..,;;...................''.''''.................'',;;;;:::;,,,,,,'''..',,;:cccllloooloddxoodxkxddodddxxoodxddddoo
...............',;;;;coxxxkxolllc:,........';coddol:'.............:lllll;'''',,,..;coddddol:,...,;:oxkxxolloodddkOO0kdoooodddoooddoddoo
.....',,;,',,,,,,;;;,:OXXXXXXXXXXXKOd:;;,:dOKXXXXXXXOl'....,,;:;;xKXXXN0;......;lk0XXXXXXXXX0xc...,kXXXklllclold0XXKklllllooolloooooooo
'',,,,,,,,,,',,,,,,,'l0XXKkddxxxOKXXXx:;;lkKOxdllxKXXKd;;;;:::lkKXXXXXX0c..'',o0XXXKkdddoxOKXXO:..,OXXKo,;;:clcdKXX0dccc::ccccccclllccl
''',,'',,,,''''''''''oXXXk:,,,;,:OXXXx;,;,;c;,,,,c0XXXd,',;,;d0XX0KXXXXkc:::oOXXX0xl:::;;:coxoc:;'lKXXO,..'.',;xXXXOc,,''',,;,,;:cc:::c
'''''''''''''''''''.,xXXXOdoooddOKXKk:'',,'':oddx0XXKx;''',lkKXKdo0NXXKo::::xXXX0o;;::;;;:::;::::cOXXX0dlllllooOXXKo'...........,;;;,,;
'...'...''....'.....;OXXXXXXXXXXXXXx;...''.'oXNXXXXKo,''.;dKXKxc,lKXXX0c,;,cOXXXd;,,;;;,;;,;;;;::l0XXXXXXXXXXXXXXX0c',;clc:;,;;........
....................cKXX0ollllodOXXKo'......:oooxKXX0c..cOXXXOolokXXXX0ol;.:OXXXd''',,,,,''',,,,,l0XXKOkkxkxxk0XXX0lclllccclloo:,'..,,'
....................dXXXd'......oKXXk'...'.......dXXXx.'kNXXXXXXXXXXXXXNK:.'dXXX0c'..''''';l:,'',dKXXOl:::::clkXXXkc::cc::::ccc:c:;;;;,
...................'kXXXkcccclld0XXKo..;d0kl:;;:o0XXKl..clllllllxKXXXKdlc...'xXXXKxlc::clx0X0o'.,xXXXx;,,;;;:l0XXKd::::::;;::c:;;;,''..
...................;0NXXXXXXXXXXXKkc...l0XXXXKKXXXKOc...........cKXXXO;.......ckKXXXXXXXXXX0d:..;OXXKc....',;oKXX0c,'''',',;;;;'.......
...................,ooooooooooolc,...',,:lx000KKKOd;...,:;......:k000Oxooc;;;:clllodxxxdol:'....'lddl'......':oodc'.........'.........,
.................................':codxxooddxkxxkxxocodxkkxdoooccododdxxddxkxxkxoc;;;,..''',;'...........'...........'...........';coox
................................,cxxxkxdxxdodxddodxddxkxdxkxdddoooooddodddxxdxkxoodddollddooddo:'.,cl:::ll:'..':lc::lollccc,'..'cdxdxxx
....................;c:;;col:;:llooxkxxdodxdoodxdolodxxoloddkdlooodxdodxxddddddxdododdoooooooddollooddoolllc:clodxdooodxddxxdlooxxddkOk
::;;;;:;'...';;cc:cloodoodooolllllloxxdllddoxdoddooooddoddooddddolldooddloollllooloooooddoloolodolloooodoooolooloooolodxxdodooooxxoodxo
dlclocldolllccclooolloolol:cooolccclllollodooooollooccloollodddoloolllolldlcllooooodocclooloolllllollloxolollccoollooolodloollllddlcodl
:lccllolcoool::cclllllc:ccccllcc::lccldollccc:lolcllcllol:cclooollllloolllccldllllll::cc:ccloodolcccccloccc:ccccoolllooooodoloccddlcddo
cccccc::c:cllc:::c::cccclclc:cc;;:::::cccc:;::cllccc:clllc:lcccclclllool::ccclc:ll::cccc:::ccccccc:::lllccc:::cclccllcccloollcc:looooll
c:,,,,;;:;;;::;;,,;;:::::::;,,;,;:;;;::::cc::;,;:cc::cllcc:clol::;:cccllc:;:cc:;:;;cl:;;;;;;;;,;c:;;;:clc;;::::;;;;:cccccll:clc:ldolc;:
,,,,,,,,'',,,,,,;:;,,,,',,,,,,;,,,''',,,,,'',,,,,;:;;:::;:;;cc::;;::::::;,,,',;,,',;:;,''',,,,,;,'',,',,,,,,,;,,,,,;;;:;:clc:c:cddc:ccc
..'...........''''''....''...''..............'..'''''';:;,,',,',,,,,;:,,,'''..'...''.....''...''..'''''.........'..',;;,,,,,:;,:ol;;;;,
...........................................................''''''''..'..'.'...........................................'.','''..;dc,'''.
,;;;;;;;;,,,''''.......'',,;;::cccccccccccc::;;,'..............................',;;::cccc::;;,,'''''',,;;:::cc:;,'.............lx:'....
0KK0KKKK0000OOOOOkOOOOOO0000KKKKKKKKKKKKKKKKKKKK0Okdolc:;;,'''''',,,,;::clodxkO00KKKKKKKXKKKKK00000000KKKKKKKKKKKOkdl:,.......cOx,....;
KKKKKKKK00KKK0KKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKK0KKKKK00K00000000KK0KKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKK0Oxddddk0Ko..:ok0
KKKKKKKKKKKKKKK00KKKKKKKKKKKK0KKKKKKKKK0KKKKKKKKKKK0KKK00KKKKK00KKKKKK00KKKKKKKKKKKKKKKKKKKK00KKKKKKKKKK00KKKKKKKKKKKKKKKKKKK0KKl'oKK00
*/

pragma solidity ^0.8.2;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721Enumerable.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721Royalty.sol";
import "@openzeppelin/contracts/access/Ownable.sol";

import "@openzeppelin/contracts/interfaces/IERC20.sol";
import "@openzeppelin/contracts/interfaces/IERC721.sol";

import "@openzeppelin/contracts/utils/cryptography/MerkleProof.sol";

import "./BeachLibrary.sol";

interface IERC20Burnable {
  function burnFrom(address account, uint256 amount) external;
}

interface ISeafood {
  function decimals() external view returns (uint8);
}

contract Beach is ERC721, ERC721Enumerable, ERC721Royalty, Ownable {
  bool private _revealed = false;
  string private _baseURIPath = "https://ipfs.io/ipfs/";
  string private _placeholderURI = "QmZc5HkKqUf1UiL9xqpanvbETB83Ter21AqMRUo1mbv9PN";
  string[2] private _revealedPath = ["", ""];
  bool private _freezeBasePath = false;

  bytes32 public waveListMerkleRoot;
  uint public MAX_SUPPLY = 1337;
  uint public MAX_PER_WALLET = 5;
  uint public MAX_PER_TX = 3;
  string public provenance = "e36178f2da4018955176de7fc70fa1fdc0dc0679f36d42f60d5a6cafe9691ba1";

  address private _lobster;
  address private _seafood;

  uint private _mintBlock;

  string[] private _TRAITS;
  string[] private _DICT;

  mapping(uint256 => string) private _beachNames;
  mapping(uint256 => uint16[8]) private _beachMetadata;
  mapping(string => bool) private _existingNames;

  uint private AMOUNT_NEEDED_FOR_NAME_CHANGE = 50;

  // PaymentSplitter
  mapping(address => uint8) private _shares;
  mapping(address => uint256) private _released;
  mapping(address => uint256) private _balances;
  address[] private _payees;
  uint256 private _totalShares;
  uint256 private _totalSplit;
  uint256 private _amountReleased;

  event PayeeAdded(address indexed account, uint8 shares);
  event PayeeUpdated(address indexed account, uint8 shares);
  event PaymentReleased(address indexed account, uint256 amount);
  event Revealed();
  event NameChanged(uint256 tokenId, string name);

  constructor(address lobster_, uint blocksBeforeTheMint_, string[] memory traits_) ERC721("B34CH DAO", "B34CH") {
    _lobster = lobster_;
    _setDefaultRoyalty(address(this), 1000);
    _mintBlock = block.number + blocksBeforeTheMint_;
    _TRAITS = traits_;
  }

  function setMerkleRoot(bytes32 waveListRoot_) external onlyOwner {
    waveListMerkleRoot = waveListRoot_;
  }

  function setSeafood(address seafood_) external onlyOwner {
    _seafood = seafood_;
  }

  receive() external payable {
  }

  /**
   * @dev Returns the wallet of a given wallet. Mainly for ease for frontend devs.
   * @param wallet_ The wallet to get the tokens of.
   */
  function walletOfOwner(address wallet_)
  public
  view
  returns (uint256[] memory)
  {
    uint256 tokenCount = balanceOf(wallet_);

    uint256[] memory tokensId = new uint256[](tokenCount);
    for (uint256 i; i < tokenCount; i++) {
      tokensId[i] = tokenOfOwnerByIndex(wallet_, i);
    }
    return tokensId;
  }

  modifier pathNotFrozen() {
    require(!_freezeBasePath, "B: Base Path frozen");
    _;
  }

  // Function intended to be triggered by the DAO in case the Gateway or Network has an issue
  function updateBaseURI(string memory uri_) external onlyOwner pathNotFrozen {
    _baseURIPath = uri_;
  }

  function freezeBasePath() external onlyOwner pathNotFrozen {
    _freezeBasePath = true;
  }

  function getRevealedPath(uint256 tokenId_) internal view returns (string memory) {
    return tokenId_ < 1000 ? _revealedPath[0] : _revealedPath[1];
  }

  function contractURI() public view returns (string memory) {
    return BeachLibrary.buildContractURI(address(this));
  }

  /**
   * @dev Returns the metadata for a token Id
   * @param tokenId_ The tokenId to return the metadata for.
   */
  function tokenURI(uint256 tokenId_)
  public
  view
  override
  returns (string memory)
  {
    _exists(tokenId_);

    return BeachLibrary.buildTokenURI(
      tokenId_,
      beachName(tokenId_),
      BeachLibrary.getImagePath(tokenId_, _revealed, _baseURIPath, getRevealedPath(tokenId_), _placeholderURI, true),
      BeachLibrary.getImagePath(tokenId_, _revealed, _baseURIPath, getRevealedPath(tokenId_), _placeholderURI, false),
      _revealed ? BeachLibrary.hashToMetadata(tokenId_, _TRAITS, _DICT, _beachMetadata) : '[]');
  }

  modifier revealedStatus(bool revealStatus_) {
    require(_revealed == revealStatus_, string(abi.encodePacked("B: REVEAL status is not ", revealStatus_ ? "TRUE" : "FALSE")));
    _;
  }

  function isMintLive() public view returns (bool) {
    return block.number > _mintBlock;
  }

  function gimmeBeaches(uint8 count_, bytes32[] calldata merkleProof_) external payable {
    require(block.number > _mintBlock, "B: Mint block not ready");
    require(count_ > 0 && count_ <= MAX_PER_TX, "B: Too many beaches for this tx");
    require(balanceOf(_msgSender()) + count_ <= MAX_PER_WALLET, "B: Too many beaches");
    require(msg.sender == tx.origin, "B: No contracts");

    uint mintPrice = getMyPriceForNextMint(merkleProof_);
    require(msg.value == mintPrice * count_, "B: Minting amount incorrect");

    for (uint8 i = 0; i < count_; i++) {
      _mintABeach(_msgSender());
    }
  }

  function amIWaveListed(bytes32[] calldata merkleProof_) public view returns (bool) {
    uint lobsterBalance = resolveLobster(_msgSender());
    bool isWaveList = false;

    if (merkleProof_.length > 0) {
      isWaveList = MerkleProof.verify(merkleProof_, waveListMerkleRoot, keccak256(abi.encodePacked(_msgSender())));
    }

    return lobsterBalance >= 1 || isWaveList;
  }

  function safeMint(address to) public onlyOwner {
    _mintABeach(to);
  }

  function _mintABeach(address to) internal {
    require(totalSupply() < MAX_SUPPLY, "B: max supply reached");
    _safeMint(to, totalSupply());
  }

  /**
   * Name management
   */
  function setName(uint tokenId_, string memory name_) external {
    address owner = IERC721(address(this)).ownerOf(tokenId_);
    require(owner == _msgSender(), "B: You do not own this token");
    require(BeachLibrary.validateName(name_), "B: Name is not valid");
    require(nameExists(name_) == false, "B: Name requested is already used");

    // The setName process is free until seafood exists
    if (_seafood != address(0x0)) {
      require(resolveSeafoodBalance(_msgSender()) > AMOUNT_NEEDED_FOR_NAME_CHANGE, "B: You do not have enough balance");
      // Burn $BEACH
      _burnSeafoodToken(AMOUNT_NEEDED_FOR_NAME_CHANGE);
    }

    _setName(tokenId_, name_);
  }

  function _setName(uint tokenId_, string memory name_) private {
    if (bytes(_beachNames[tokenId_]).length > 0) {
      _existingNames[_beachNames[tokenId_]] = false;
    }
    _beachNames[tokenId_] = name_;
    _existingNames[BeachLibrary.toLower(name_)] = true;
    emit NameChanged(tokenId_, name_);
  }

  function nameExists(string memory name_) public view returns (bool) {
    return _existingNames[BeachLibrary.toLower(name_)];
  }

  function beachName(uint tokenId_) public view returns (string memory) {
    bool isEmpty = keccak256(abi.encodePacked(_beachNames[tokenId_])) == keccak256(abi.encodePacked("")) || keccak256(abi.encodePacked(_beachNames[tokenId_])) == keccak256(bytes(BeachLibrary.toString(tokenId_)));
    return string(abi.encodePacked("Beach ", isEmpty ? string(abi.encodePacked("#", BeachLibrary.toString(tokenId_))) : _beachNames[tokenId_]));
  }

  /**
   * $SEAFOOD management
   */
  function resolveSeafoodBalance(address account) internal view returns (uint) {
    return IERC20(_seafood).balanceOf(account);
  }

  // Takes a high order amount (for example 10) and returns fully decimal'ed value (10 * 10 ** 18)
  function _toFullDecimals(uint amount_) private view returns (uint) {
    return amount_ * 10 ** ISeafood(_seafood).decimals();
  }

  function _burnSeafoodToken(uint amount_) private returns (bool) {
    uint __amount = _toFullDecimals(amount_);
    require(resolveSeafoodBalance(_msgSender()) >= __amount, "B: Not enough balance");
    IERC20Burnable(_seafood).burnFrom(_msgSender(), __amount);
    return true;
  }

  // The following functions are overrides required by Solidity.

  function _burn(uint256 tokenId) internal override(ERC721, ERC721Royalty) {}

  function _beforeTokenTransfer(address from, address to, uint256 tokenId)
  internal
  override(ERC721, ERC721Enumerable)
  {
    super._beforeTokenTransfer(from, to, tokenId);
  }

  function supportsInterface(bytes4 interfaceId)
  public
  view
  override(ERC721, ERC721Enumerable, ERC721Royalty)
  returns (bool)
  {
    return super.supportsInterface(interfaceId);
  }

  // Beach-specific functions

  /***************************
   *      Reveal / Mint      *
   ***************************/

  function setDict(string[] memory dict) public onlyOwner revealedStatus(false) {
    _DICT = dict;
  }

  function setMetadata(uint16[2] calldata range, uint16[][] calldata metadata) external onlyOwner returns (bool) {
    require(range[0] >= 0 && range[0] < range[1] && range[1] <= MAX_SUPPLY, "B: Range incorrect");
    for (uint i = 0; i < range[1] - range[0]; i++) {
      uint tokenId_ = i + range[0];

      _beachMetadata[tokenId_][0] = metadata[i][0];
      _beachMetadata[tokenId_][1] = metadata[i][1];
      _beachMetadata[tokenId_][2] = metadata[i][2];
      _beachMetadata[tokenId_][3] = metadata[i][3];
      _beachMetadata[tokenId_][4] = metadata[i][4];
      _beachMetadata[tokenId_][5] = metadata[i][5];
      _beachMetadata[tokenId_][6] = metadata[i][6];
      _beachMetadata[tokenId_][7] = metadata[i][7];
    }

    return true;
  }

  function revealState() public view returns (bool) {
    return _revealed;
  }

  function reveal(string[] calldata paths) external onlyOwner {
    require(_revealed == false, "B: already revealed");
    _revealed = true;
    _revealedPath[0] = paths[0];
    _revealedPath[1] = paths[1];
    emit Revealed();
  }

  function resolveLobster(address target) public view returns (uint) {
    return IERC721(_lobster).balanceOf(target);
  }

  function getMintPrice(bool isWaveList_) public view returns (uint) {
    uint currentSupply = totalSupply();

    if (currentSupply < 137) {
      if (isWaveList_) {
        return 0 ether;
      }
      return 1 ether;
    } else if (currentSupply >= 137 && currentSupply < 317) {
      if (isWaveList_) {
        return 0.037 ether;
      }
      return 0.073 ether;
    } else if (currentSupply >= 317 && currentSupply < 713) {
      if (isWaveList_) {
        return 0.073 ether;
      }
      return 0.1 ether;
    } else if (currentSupply >= 713) {
      if (isWaveList_) {
        return 0.1 ether;
      }
      return 0.1337 ether;
    } else {
      return 1 ether;
    }
  }

  function getMyPriceForNextMint(bytes32[] calldata merkleProof_) public view returns (uint) {
    bool isWaveList = amIWaveListed(merkleProof_);
    return getMintPrice(isWaveList);
  }

  /**************************
   *    Payment Splitter    *
   **************************/

  function _addPayee(address account_, uint8 shares_) private {
    require(account_ != address(0), "PS: No 0");
    require(shares_ > 0, "PS: Shares = 0");
    require(_totalShares + shares_ < 256, "PS: Max shares");
    require(_shares[account_] == 0 && _released[account_] == 0 && _balances[account_] == 0, "PS: Account exists");
    require(_payees.length < 256, "PS: Too many shareholders");

    // Flush outstanding balances to ensure that new payees don't mess things up
    _splitOutstandingBalance();

    _payees.push(account_);
    _shares[account_] = shares_;
    _totalShares += shares_;

    emit PayeeAdded(account_, shares_);
  }

  function addPayee(address account_, uint8 shares_) external onlyOwner {
    _addPayee(account_, shares_);
  }

  function addPayeesBatch(address[] calldata accounts_, uint8[] calldata shares_) external onlyOwner {
    for (uint8 i = 0; i < accounts_.length; i++) {
      _addPayee(accounts_[i], shares_[i]);
    }
  }

  function _updatePayee(address account_, uint8 shares_) private {
    require(account_ != address(0), "PS: Address can't be 0");
    require(_released[account_] > 0 || _balances[account_] > 0, "PS: Account doesn't exist");
    require(_totalShares - _shares[account_] + shares_ < 256, "PS: Max shares reached");

    // Flush outstanding balances to ensure that new payees don't mess things up
    _splitOutstandingBalance();

    uint8 sharesBefore = _shares[account_];

    // Update shares
    _shares[account_] = shares_;

    // Adjust total shares
    _totalShares = _totalShares - sharesBefore + shares_;

    emit PayeeUpdated(account_, shares_);
  }

  function updatePayee(address account_, uint8 shares_) external onlyOwner {
    _updatePayee(account_, shares_);
  }

  function updatePayeesBatch(address[] calldata accounts_, uint8[] calldata shares_) external onlyOwner {
    for (uint8 i = 0; i < accounts_.length; i++) {
      _updatePayee(accounts_[i], shares_[i]);
    }
  }

  function _split(uint amount) private {
    for (uint8 i = 0; i < _payees.length; i++) {
      // Only calculate if the shares are not 0 to avoid un-needed calculations
      if (_shares[_payees[i]] > 0) {
        uint256 amountSplit = amount * _shares[_payees[i]] / _totalShares;
        _balances[_payees[i]] += amountSplit;
        _totalSplit += amountSplit;
      }
    }
  }

  function _splitOutstandingBalance() internal {
    uint outstandingBalance = address(this).balance - (_totalSplit - _amountReleased);
    _split(outstandingBalance);
  }

  function split() external onlyAccountOrOwner(_msgSender()) {
    _splitOutstandingBalance();
  }

  // dev: This is not supposed to be used. More like an emergency withdraw
  //      It assumes that owner() is the DAO address.
  function withdrawEthFunds() external onlyOwner {
    Address.sendValue(payable(owner()), address(this).balance);
  }

  function withdrawERC20Funds(address ERC20_) external onlyOwner {
    IERC20(ERC20_).transfer(owner(), IERC20(ERC20_).balanceOf(address(this)));
  }

  function release(address payable account) external onlyAccountOrOwner(account) {
    require(_balances[account] > 0, "PS: No balance");

    _splitOutstandingBalance();

    // Non-re-entrant pre-tx
    uint256 amountToSend = _balances[account];
    _balances[account] = 0;

    Address.sendValue(account, amountToSend);

    // Post-tx book-keeping
    _released[account] = _released[account] + amountToSend;
    _amountReleased = _amountReleased + amountToSend;

    emit PaymentReleased(account, amountToSend);
  }

  function accountBalance(address account) public view onlyAccountOrOwner(account) returns (uint256) {
    return _balances[account];
  }

  function balanceAvailable() public view returns (uint256) {
    return address(this).balance;
  }

  function totalReceived() public view returns (uint256) {
    return address(this).balance + _amountReleased;
  }

  modifier onlyAccountOrOwner(address account) {
    require(owner() == _msgSender() || account == _msgSender(), "Not the owner or account owner");
    _;
  }
}
